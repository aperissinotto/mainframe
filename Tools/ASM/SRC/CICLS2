*******************************************************************   @
*                                                                 *   @
* NOME DO MODULO..........: CICLST                                *   @
* TITULO..................: TASK MAE LISTENER.                    *   @
*                                                                 *   @
* AREA RESPONSAVEL........: SODC                                  *   @
*                                                                 *   @
* TIPO DE MODULO..........: PROGRAMA ASSEMBLER CICS               *   @
*                                                                 *   @
* DESCRICAO RESUMIDA......: FICA EM LISTENER.                     *   @
*                                                                 *   @
* FUNCIONALIDADES.........:                                       *   @
* - FICAR EM LISTENER.                                            *   @
*                                                                 *   @
* ARQUIVOS ACESSADOS......:                                       *   @
*                                                                 *   @
* MODULOS CHAMADORES......:                                       *   @
* - NENHUM                                                        *   @
*                                                                 *   @
* MODULOS CHAMADOS........:                                       *   @
* - NENHUM                                                        *   @
*                                                                 *   @
* MACROS                                                          *   @
* - NENHUMA                                                       *   @
*                                                                 *   @
* DSECTS DE MAPEAMENTO:                                           *   @
* - DFHEISTG - COPY BOOK QUE DEFINE A AREA DE WORKING             *   @
*                                                                 *   @
* DESCRICAO DE USO DOS REGISTRADORES:                             *   @
*  - R11: BASE DO EIB CONTROL BLOCK                               *   @
*  - R12/R10: BASE DO PROGRAMA                                    *   @
*  - R13: BASE DA WORKING                                         *   @
*                                                                 *   @
* PARAMETROS DE ENTRADA:                                          *   @
*  - NAO HA.                                                      *   @
*                                                                 *   @
* PARAMETROS DE SAIDA:                                            *   @
*  - NAO HA.                                                      *   @
*                                                                 *   @
*******************************************************************   @
*                    CONTROLE DE ALTERACOES                       *   @
*-----------------------------------------------------------------*   @
*  DATA    * VER * AUTOR    * DESCRICAO                           *   @
*-----------------------------------------------------------------*   @
* XX/11/21 * XXX * ALEXANDRE* VERSAO INICIAL                      *   @
*          * 001 *          *                                     *   @
*-----------------------------------------------------------------*   @
         IEABRCX DEFINE                                           *   @
CICLST   RSECT
CICLST   DFHEIENT CODEREG=(R12),                                       X
               DATAREG=(R13),                                          X
               EIBREG=(R11),                                           X
               STATIC=CISTAT
CICLST   AMODE 31
CICLST   RMODE ANY
*-----------------------------------------------------------------*   @
*        CHAMA O INIT                                             *   @
*-----------------------------------------------------------------*   @
         XC    WERRNO,WERRNO
         XC    WRETCODE,WRETCODE
         MVC   WSUBTASK(L'EIBTASKN),EIBTASKN
         MVI   WSUBTASK+L'EIBTASKN,C'L'
         CALL  EZACICSO,                                               X
               (LSOCINI,                                               X
               LMAXSOC,                                                X
               LIDENT,                                                 X
               WSUBTASK,                                               X
               LMAXSNO,                                                X
               WERRNO,WRETCODE),                                       X
               VL,MF=(E,PLIST)
*---------------------------------------------------------------------*
         EXEC  CICS WRITEQ TS QUEUE(=CL8'TESTE') FROM(WERRNO)          X
               LENGTH(=H'4')
         EXEC  CICS WRITEQ TS QUEUE(=CL8'TESTE') FROM(WRETCODE)        X
               LENGTH(=H'4')
*-----------------------------------------------------------------*   @
*        PEDE UM SOCKET                                           *   @
*-----------------------------------------------------------------*   @
         XC    WERRNO,WERRNO
         XC    WRETCODE,WRETCODE
         CALL  EZACICSO,                                               X
               (LSOCSOK,                                               X
               LAFINET,                                                X
               LSOCTYPE,                                               X
               LPROTO,                                                 X
               WERRNO,WRETCODE),                                       X
               VL,MF=(E,PLIST)
*---------------------------------------------------------------------*
         EXEC  CICS WRITEQ TS QUEUE(=CL8'TESTE') FROM(WERRNO)          X
               LENGTH(=H'4')
         EXEC  CICS WRITEQ TS QUEUE(=CL8'TESTE') FROM(WRETCODE)        X
               LENGTH(=H'4')
*-----------------------------------------------------------------*   @
*        FAZ UM BIND                                              *   @
*-----------------------------------------------------------------*   @
         XC    WERRNO,WERRNO
         XC    WRETCODE,WRETCODE
         MVC   WSD(2),WRETCODE+2
         MVHHI WFAMILY,2
         MVHHI WPORT,3005
         MVI   WIP,192
         MVI   WIP+1,168
         MVI   WIP+2,25
         MVI   WIP+3,210
         CALL  EZACICSO,                                               X
               (LSOCBND,                                               X
               WSD,                                                    X
               WNAME,                                                  X
               WERRNO,WRETCODE),                                       X
               VL,MF=(E,PLIST)
*---------------------------------------------------------------------*
         EXEC  CICS WRITEQ TS QUEUE(=CL8'TESTE') FROM(WERRNO)          X
               LENGTH(=H'4')
         EXEC  CICS WRITEQ TS QUEUE(=CL8'TESTE') FROM(WRETCODE)        X
               LENGTH(=H'4')
*-----------------------------------------------------------------*   @
*        FAZ UM LISTEN                                            *   @
*-----------------------------------------------------------------*   @
         XC    WERRNO,WERRNO
         XC    WRETCODE,WRETCODE
         CALL  EZACICSO,                                               X
               (LSOCLST,                                               X
               WSD,                                                    X
               LBACK,                                                  X
               WERRNO,WRETCODE),                                       X
               VL,MF=(E,PLIST)
*---------------------------------------------------------------------*
         EXEC  CICS WRITEQ TS QUEUE(=CL8'TESTE') FROM(WERRNO)          X
               LENGTH(=H'4')
         EXEC  CICS WRITEQ TS QUEUE(=CL8'TESTE') FROM(WRETCODE)        X
               LENGTH(=H'4')
*-----------------------------------------------------------------*   @
*        FIM DO PROGRAMA                                          *   @
*-----------------------------------------------------------------*   @
CPFIM    DS   0H                                                  *   @
*        EXEC  CICS SEND CONTROL                                       X
               ERASE
*                                                                 *   @
*        EXEC CICS SEND TEXT FROM(LFILEN)
         XR    R15,R15              * SETA RC=0                   *   @
         DFHEIRET RCREG=R15         * RETORNA P/ CICS             *   @
*-----------------------------------------------------------------*   @
*        AREA DE LITERAIS                                         *   @
*-----------------------------------------------------------------*   @
CISTAT   DS   0D                                                  *   @
         LTORG                                                    *   @
LFILEN   DC    C'SUPICES '                                        *   @
LMAXSOC  DC    H'50'
LSOCINI  DC    CL16'INITAPI         '
LSOCSOK  DC    CL16'SOCKET          '
LSOCBND  DC    CL16'BIND            '
LSOCLST  DC    CL16'LISTEN          '
LIDENT   DC    CL16'TCPIP   CICSTS54'
LMAXSNO  DC    F'49'
LAFINET  DC    F'2'
LSOCTYPE DC    F'1'
LPROTO   DC    F'0'
LBACK    DC    F'10'
*                                                                 *   @
         COPY  DFHAID                                             *   @
         COPY  DFHBMSCA                                           *   @
*-----------------------------------------------------------------*   @
*        DSECT'S                                                  *   @
*-----------------------------------------------------------------*   @
*        AREA DE WORKING                                          *   @
*-----------------------------------------------------------------*   @
DFHEISTG DSECT                                                    *   @
         ORG   DFHEIUSR                                           *   @
*                                                                 *   @
WSD      DS    H
WSUBTASK DS    CL8
WERRNO   DS    F
WRETCODE DS    F
*                                                                 *   @
WNAME    DS   0CL16
WFAMILY  DS    H
WPORT    DS    H
WIP      DS    F
         DS    CL8
WNAMEL   EQU   *-WNAME
*                                                                 *   @
PLIST    DS  13A
*                                                                 *   @
WLENWRK  EQU   *-DFHEISTG           * TAMANHO TOTAL DA AREA       *   @
*-----------------------------------------------------------------*   @
*        EQUATES DE REGISTRADORES                                 *   @
*-----------------------------------------------------------------*   @
R0       EQU   0                                                  *   @
R1       EQU   1                                                  *   @
R2       EQU   2                                                  *   @
R3       EQU   3                                                  *   @
R4       EQU   4                                                  *   @
R5       EQU   5                                                  *   @
R6       EQU   6                                                  *   @
R7       EQU   7                                                  *   @
R8       EQU   8                                                  *   @
R9       EQU   9                                                  *   @
R10      EQU   10                                                 *   @
R11      EQU   11                                                 *   @
R12      EQU   12                                                 *   @
R13      EQU   13                                                 *   @
R14      EQU   14                                                 *   @
R15      EQU   15                                                 *   @
         END   CICLST                                             *   @
